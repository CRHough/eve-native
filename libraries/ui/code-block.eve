# Code Blocks

Code blocks are intances of CodeMirror editors that can be used to display and 
edit Eve code.

We create a code-block with a default value and id.

search
  block = [#ui/code-block]
  //not(block = [#init])
  value = if block.value then block.value else ""
  init = if block.init then block.init else "0"
watch client/websocket
  ("code-block/create", block, value, init)
end

Code blocks are given tagged as `#html/element` so they will be rendered in the
DOM. We replace this element with a CodeMirror instance, and sync its value 
property with the value stored in Eve.

search
  block = [#ui/code-block]
bind
  block <- [#html/element tagname: "div"]
end

When we've gotten an update from the client, apply it to the original record,
keeping them in sync.

search
  change = [#ui/code-block/change block new-value]
  block = [#ui/code-block value]
commit
  change := none
  block.value := new-value
end

If `value` changes in Eve, we need to sync that in the rendered version

search
  block = [#ui/code-block value]
  not([#ui/code-block/change new-value: value])
watch client/websocket
  ("code-block/update", block, value)
end
