Domains
------------------------------------------------------

Domains help us represent the valid values of some value within a block. Knowing
the domain for both the inputs and outputs of a block help us dramatically prune
the otherwise very cyclic graph that most Eve programs end up forming. There are
a few types of domains:

    commit
      [#define/domain type:"unknown" sort:1]
      [#define/domain type:"removed" sort:2]
      [#define/domain type:"string" sort:3]
      [#define/domain type:"record" sort:4]
      // numbers can also optionally have a from/to to help be even more
      // specific about their bounds
      [#define/domain type:"number" sort:5]
      [#define/domain type:"multi-type" sort:6]
    end

One of the most important things for us to be able to do with domains is take a
set of them and find out what the most specific domain we have is. The first
step there is to check if we have multiple types.

    search
      set = [#domain-set domain: [#domain type]]
      [#define/domain type sort > 2]
      count = gather/count[for:type per:set]
      count > 1
    bind
      set.domain += [#domain type:"multi-type"]
    end

To figure out what our final domain is given all the potential options we have,
we look for the type with the highest defined sort.

    search
      set = [#domain-set domain: [#domain type]]
      [#define/domain type sort]
      gather/top[for:sort per:set limit:1]
    bind
      set.final += [#domain/final set type]
    end

Then, if it's a number we need to try and capture our numeric bounds by taking
the largest `from` and smallest `to`.

    search
      set = [#domain-set final domain:[#domain from]]
      gather/top[for:from per:set limit:1]
    bind
      final.from += from
    end

    search
      set = [#domain-set final domain:[#domain to]]
      gather/bottom[for:to per:set limit:1]
    bind
      final.to += to
    end

We also need to be able to do some basic arithmetic on domains to propagate
constraints forward. We'll represent these as records with the operation as a
tag and two extra attributes `a` and `b` representing the operands.

    search
      domain = if d = [#domain/add] then d
               if d = [#domain/sub] then d
               if d = [#domain/mul] then d
               if d = [#domain/div] then d
    bind
      domain <- [#domain type:"number"]
    end

Add and subtract are exactly like what you'd expect:

    search
      add = [#domain/add a b]
    bind
      add.from += a.from + b.from
    end

    search
      add = [#domain/add a b]
    bind
      add.to += a.to + b.to
    end

    search
      sub = [#domain/sub a b]
    bind
      sub.from += a.from - b.from
    end

    search
      sub = [#domain/sub a b]
    bind
      sub.to += a.to - b.to
    end

Multiplication and division have to deal with the fact that multiplying or
dividing by a negative could potentially swap the from and to. To handle this,
we'll set a maybe-from and maybe-to attribute and then resolve them to `to` and
`from` later.

    search
      mult = [#domain/mul a b]
    bind
      mult.maybe-from += a.from * b.from
    end

    search
      mult = [#domain/mul a b]
    bind
      mult.maybe-to += a.to * b.to
    end

    search
      mult = [#domain/mul a b]
      to = if a.to < 0, not(b.to) then "-Infinity"
           else if b.to < 0, not(a.to) then "-Infinity"
    bind
      mult.maybe-to += to
    end

    search
      div = [#domain/div a b]
    bind
      div.maybe-from += a.from / b.from
    end

    search
      div = [#domain/div a b]
    bind
      div.maybe-to += a.to / b.to
    end

    search
      div = [#domain/div a b]
      to = if a.to < 0, not(b.to) then "-Infinity"
           else if b.to < 0, not(a.to) then "-Infinity"
    bind
      div.maybe-to += to
    end

To resolve the maybes we created above, we just need to check if the from is
actually greater than the to or if the to is less than the from. If from or to
are missing then it represents infinity in that direction and we just take the
maybe value as it is.

    search
      domain = [#domain maybe-to]
      to = if domain.maybe-from > maybe-to then domain.maybe-from
           else if maybe-to = "-Infinity" then domain.maybe-from
           else if maybe-to != "-Infinity" then maybe-to
    bind
      domain.to += to
    end

    search
      domain = [#domain maybe-from]
      from = if domain.maybe-to < maybe-from then domain.maybe-to
             else if domain.maybe-to != "-Infinity" then maybe-from
             else if not(domain.maybe-to) then maybe-from
    bind
      domain.from += from
    end

It's also useful to be able to take an arbitrary value and turn it into a domain

    search
      domain = [#domain/from-value value]
      "number" = eve/type-of[value]
    bind
      domain <- [#domain type:"number" from:value to:value]
    end

    search
      domain = [#domain/from-value value]
      "string" = eve/type-of[value]
      not(lookup[entity:value, attribute:"tag"])
    bind
      domain <- [#domain type:"string"]
    end

    search
      domain = [#domain/from-value value]
      value = [#register]
      [#domain-set register:value final]
    bind
      domain <- [#domain/from-final #domain type:final.type final]
    end

    search
      domain = [#domain/from-final final]
    bind
      domain.from += final.from
    end

    search
      domain = [#domain/from-final final]
    bind
      domain.to += final.to
    end


Tag info
------------------------------------------------------

We want to keep information that we know about tags based on all the blocks in
the programs we see so that we can use the combined information to make some
optimizations. So first step is to find all the tags, which are hidden away in
scans and inserts.

    search
      [#scan a:"tag" v:tag-name]
    bind
      [#tag-info tag-name]
    end

    search
      [#insert a:"tag" v:tag-name]
    bind
      [#tag-info tag-name]
    end


Blocks and constraints
------------------------------------------------------

There's some useful information we can adorn blocks with, like how many
constraints they have.

    search
      block = [#block constraint]
      num-constraints = gather/count[for:constraint per:block]
    bind
      block <- [num-constraints]
    end

We need to gather what tag each e belongs to in each block. We then
need to gather our inputs and outputs for the block based on what tag they
represent.

    search
      [#scan block e a:"tag" v]
    bind
      [#block-tag block e tag-name:v]
    end

    search
      [#block-tag block e tag-name]
      [#scan block e a]
    bind
      [#block-input block tag-name a]
    end

    search
      (block e v) = if [#insert block e a:"tag" v] then (block e v)
                    else if [#remove block e a:"tag" v] then (block e v)
    bind
      [#block-tag block e tag-name:v]
    end

    search
      [#block-tag block e tag-name]
      (block a) = if [#insert block e a] then (block a)
                    else if [#remove block e a] then (block a)
    bind
      [#block-output block tag-name a]
    end

Match all the inputs with the outputs per block and create a link between those
blocks. Note that recursive blocks will have themselves as both inputs and
outputs.

    search
      [#block-input block:to tag-name a]
      [#block-output block:from tag-name a]
    bind
      [#link from to]
    end

Find blocks that serve as roots. These are either blocks that rely on an
external tag, and therefore an event, or they are blocks without scans and act
as a setup

    search
      [#external-tags tag-name]
      [#block-input block tag-name]
    bind
      block += #root
    end

    search
      block = [#block]
      not([#scan block])
      not([#if block])
    bind
      block += #setup
    end

Each setup and each root block will ultimately turn into the starting points for
chains of execution in the compiled program.

    search
      block = [#setup]
    bind
      [#chain type:"setup" to:block]
    end

    search
      block = [#root]
      [#block-input block tag-name]
    bind
      [#chain type:"root" tag-name | to:block]
    end

We create this chain by walking the links between blocks starting at the #root
or #setup. We also want to discover cycles here, which we figure out by looking
for a link that has the same to, but a lower level

    search
      chain = [#chain to:start]
      [#link from:start to]
      start != to
    bind
      [#chain-link chain from:chain to level:1]
    end

    search
      chain = [#chain to:start]
      [#link from:start to:start]
    bind
      [#chain-link #cycle chain from:chain to:start level:1]
    end

    search
      link = [#chain-link chain to:from level]
      [#link from to]
      not(link.tag = "cycle")
      not(
        prev = [#chain-link chain to]
        prev.level <= level
      )
      next-level = level + 1
    bind
      [#chain-link chain from:link to level:next-level]
    end

    search
      cur = [#chain-link chain to:from level]
      not(link.tag = "cycle")
      [#link from to]
      prev = [#chain-link chain to]
      not(prev.tag = "cycle")
      prev.level <= level
    bind
      [#chain-link #cycle chain from:cur to level]
    end

Turn output constraints into their "real" equivalents

    search
      [#insert block e a v]
      [#remove-attribute block e a]
    bind
      [#real/set block e a v]
    end

    search
      [#function block op:"gen-id" output: e]
      [#insert block e a v]
    bind
      [#real/create block e | attribute: [a v]]
    end

    search
      [#insert block e a v]
      not([#set block e a])
      not([#create block e])
    bind
      [#real/update block e a v]
    end

    search
      [#remove block e a v]
      not([#set block e a])
    bind
      [#real/remove block e a v]
    end


Block heuristics
------------------------------------------------------

Event-like things follow a common pattern where they exist for exactly one
frame. This is accomplished by looking for a tag and then removing any full
record that matches. We've seen these called "pulses" elsewhere, so that's the
name we'll use here. We conservatively look for these by checking for blocks
with only two constraints, one that is a tag lookup and the other that is
remove-entity for that record.

    search
      block.num-constraints = 2
      [#scan block e a:"tag" v: tag-name]
      [#remove-entity block e]
      tag-info = [#tag-info tag-name]
    bind
      block += #pulse
      tag-info += #pulse
    end


Register domains
------------------------------------------------------

In order to determine what the domains of our inputs and outputs are, we need to
figure out the domains of each register in a block. We do this by creating a
domain set for each register and then by looking through the constraints to
supply domains.

    search
      register = [#register]
    bind
      [#domain-set register]
    end

For scans we know that the e register is in the record domain, an a register is
in the string domain and a v is unknown.

    search
      [#scan e]
      set = [#domain-set register:e]
    bind
      set.domain += [#domain type:"record"]
    end

    search
      [#scan a]
      set = [#domain-set register:a]
    bind
      set.domain += [#domain type:"string"]
    end

    search
      [#scan v]
      set = [#domain-set register:v]
    bind
      set.domain += [#domain type:"unknown"]
    end

For filters we need to find out ...

    search
      [#filter op:">" left right]
      set = [#domain-set register:left]
      not(right = [#register])
    bind
      set.domain += [#domain type:"number" from:right]
    end

    search
      [#filter op:">" left right]
      set = [#domain-set register:right]
      not(left = [#register])
    bind
      set.domain += [#domain type:"number" to:left]
    end

    search
      [#filter op:"<" left right]
      set = [#domain-set register:left]
      not(right = [#register])
    bind
      set.domain += [#domain type:"number" to:right]
    end

    search
      [#filter op:"<" left right]
      set = [#domain-set register:right]
      not(left = [#register])
    bind
      set.domain += [#domain type:"number" from:left]
    end

Functions...

    search
      [#function op:"+" params: [index:1 value:left] [index:2 value:right] output]
      set = [#domain-set register:output]
    bind
      set.domain += [#domain/add
                      a: [#domain/from-value output value:left]
                      b: [#domain/from-value output value:right]]
    end

    search
      [#function op:"-" params: [index:1 value:left] [index:2 value:right] output]
      set = [#domain-set register:output]
    bind
      set.domain += [#domain/sub
                      a: [#domain/from-value output value:left]
                      b: [#domain/from-value output value:right]]
    end

    search
      [#function op:"*" params: [index:1 value:left] [index:2 value:right] output]
      set = [#domain-set register:output]
    bind
      set.domain += [#domain/mul
                      a: [#domain/from-value output value:left]
                      b: [#domain/from-value output value:right]]
    end

    search
      [#function op:"/" params: [index:1 value:left] [index:2 value:right] output]
      set = [#domain-set register:output]
    bind
      set.domain += [#domain/div
                      a: [#domain/from-value output value:left]
                      b: [#domain/from-value output value:right]]
    end


Block -> tag domains
------------------------------------------------------

For input/output tag in our block, we need to determine what the domains of the
attributes of that tag are. We'll then use this information in the chain
optimizer to remove nodes that can't actually follow after eachother.

    search
      [#scan block e a v]
      [#block-tag block e tag-name]
      input = [#block-input block tag-name a]
      [#domain-set register:v final]
    bind
      input.domain += final
    end

    search
      [#insert block e a v]
      [#block-tag block e tag-name]
      output = [#block-output block tag-name a]
      [#domain-set register:v final]
    bind
      output.domain += final
    end


Optimizing
------------------------------------------------------

While on the surface blocks tend to produce many cycles, they're often written
in such a way that if you knew the bounds of their inputs and their outputs, you
could prove that they don't actually form a cycle. Much of the optimizer is just
us using this domain information to "simulate" the program's execution and start
pruning chain-links. Instead of modifying our original chain-links, we'll create
a new set of optimized ones and any link that hasn't been removed by some
optimization rule is part of the new, optimized chain.

    search
      link = [#chain-link chain from to level]
      not([#link-removal link])
    bind
      [#chain-link/optimized chain from to level]
    end

    search
      opt = [#chain-link/optimized chain link]
      link = [#chain-link chain back-link]
      not([#link-removal link back-link])
    bind
      opt.back-link += back-link
    end

On to the link removers. The first one we'll do is that if a block has a pulse
as an input, and the from of a chain-link doesn't output that input, then the to
can't run.

    search
      link = [#chain-link from to]
      [#block-input block:to tag-name]
      [#tag-info #pulse tag-name]
      not([#block-output block:from.to tag-name a:"tag"])
    bind
      [#link-removal link reason: "Relies on a pulse that has already been removed"]
    end



Intermediate Representation (IR)
------------------------------------------------------

With a set of optimized chains, we can now generate a language agnostic IR tree
that represents the optimized program.


Code Generation
------------------------------------------------------

From the IR we can generate actual source code in some language that we can then
execute. At this point, code generation is a largely mechanical process of
looking at IR nodes and constructing code strings out of them.

    search
      (ir, js) =
        if ir = [#ir/symbol name] then (ir, name)
        if ir = [#ir/string str] then (ir, "\"{{str}}\"")
        if ir = [#ir/number num] then (ir, "{{num}}")
        if ir = [#ir/infix op left right] then (ir, "{{left.js}} {{op}} {{right.js}}")
        if ir = [#ir/let name value] then (ir, "var {{name.js}} = {{value.js}};")
        if ir = [#ir/set symbol attribute value] then (ir, "{{symbol.js}}.{{attribute.javscript}} = {{value.js}};")
        if ir = [#ir/get symbol attribute] then (ir, "{{symbol.js}}.{{attribute.js}}")
    bind
      ir <- [js]
    end

Drawing
------------------------------------------------------

    search
      [#chain-link from to chain level]
    bind
      [#ui/column #links chain | children:
        [#ui/text sort:level text: "{{level}}: {{from}} -> {{to}}"]]
    end

    search
      [#block-input block tag-name a domain]
    bind
      [#ui/column #block-tags | children:
        [#ui/text text: "INPUT: {{block}} - {{tag-name}} {{a}} {{domain.type}}"]]
    end

    search
      [#block-output block tag-name a domain]
    bind
      [#ui/column #block-output-tags | children:
        [#ui/text text: "OUTPUT: {{block}} - {{tag-name}} {{a}} {{domain.type}}"]]
    end


Test
------------------------------------------------------

    commit
      [#external-tags tag-name: ("system/timer/change")]
    end

    commit
      r1 = [#register block:b3 offset:1]
      r2 = [#register block:b3 offset:2]
      r3 = [#register block:b3 offset:3]
      b3 = [#block name:"b3"]
      [#scan block:b3 e:r1 a:"tag" v:"system/timer/change"]
      [#scan block:b3 e:r1 a:"score" v:r2]
      [#function block:b3
                 params: [index:1 value:r2] [index:2 value:1]
                 output:r3]
      [#insert block:b3 e:r1 a:"score" v:r3]

      b4r1 = [#register block:b4 offset:1]
      b4 = [#block name:"b4" | constraint:
             [#scan block:b4 e:b4r1 a:"tag" v:"system/timer/change"]
             [#remove-entity block:b4 e:b4r1]]
    end
